plugins {
    id 'groovy'
    id 'maven'
}

install {
    repositories.mavenInstaller {
        pom.groupId = 'org.strykeforce'
        pom.artifactId = 'thirdcoast'
        pom.version = '2017.1.0'
    }
}

dependencies {
    compile wpilib()
    compile ctre()
    compile navx()
    compile libs.toml4j
    compile libs.moshi
    compile libs.nanohttpd
    compile libs.dagger
    compile libs.slf4j
    compileOnly libs.jetbrainsAnnotations
    compileOnly libs.jsr305

    apt libs.daggerCompiler

    testCompile libs.spock
    testRuntime libs.logback
    testRuntime libs.bytebuddy
    testRuntime libs.objenesis
}

javadoc {
    title = "Third Coast FRC Library"
    options.overview = "src/main/java/overview.html"
    options.links = [
            "https://docs.oracle.com/javase/8/docs/api/",
            "http://first.wpi.edu/FRC/roborio/release/docs/java/",
            "http://www.ctr-electronics.com/downloads/api/java/html/",
            "http://www.kauailabs.com/public_files/navx-mxp/apidocs/java/"
    ]
    options.addBooleanOption("-allow-script-in-comments", true)
    options.footer = "<script async src=\"https://www.googletagmanager.com/gtag/js?id=UA-108186367-1\"></script>\n" +
            "<script>\n" +
            "  window.dataLayer = window.dataLayer || [];\n" +
            "  function gtag(){dataLayer.push(arguments);}\n" +
            "  gtag('js', new Date());\n" +
            "\n" +
            "  gtag('config', 'UA-108186367-1');\n" +
            "</script>\n"

}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}