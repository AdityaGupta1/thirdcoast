plugins {
    id "java"
    id "idea"
    id "net.ltgt.apt-idea" version "0.12"
    id "jaci.openrio.gradle.GradleRIO" version "2017.1.5" apply false
}

subprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }

idea {
    module {
        apt {
            addGeneratedSourcesDirs = true
            addAptDependencies = false
        }
    }
}
}

subprojects {
    apply plugin: 'jaci.openrio.gradle.GradleRIO'

    frc {
        team = "2767"
        robotClass = "org.strykeforce.thirdcoast.robot.Robot"
        rioHost = "10.27.67.2"
        deployTimeout = 2
        jvmArguments = "-Xms10m"
    }

    wpi {
        wpilibVersion = "+"
        ntcoreVersion = "+"
        opencvVersion = "3.1.0"
        cscoreVersion = "+"
        talonSrxVersion = "4.4.1.14"
    }
}

def exportedProjects = [
        ":core",
]

task allJavadoc(type: Javadoc) {
    description "Generate Javadoc API documentation for core and shared projects."
    group "Documentation"
    source exportedProjects.collect { project(it).sourceSets.main.allJava }
    classpath = files(exportedProjects.collect { project(it).sourceSets.main.compileClasspath })
    title = "Third Coast Swerve Drive API"
    /*destinationDir = file("docs/javadoc")*/
    options.overview = "core/src/main/java/overview.html"
    options.links = [
            "https://docs.oracle.com/javase/8/docs/api/",
            "http://first.wpi.edu/FRC/roborio/release/docs/java/",
            "http://www.ctr-electronics.com/downloads/api/java/html/",
            "http://www.kauailabs.com/public_files/navx-mxp/apidocs/java/"
    ]
}

task cleanJavadoc(type: Delete) {
    delete 'docs/javadoc'
}

task refreshJavadoc(type: Copy) {
    description "Generate Javadoc API documentation and copy to docs dir for publication."
    group "Documentation"
    from 'build/docs'
    into 'docs'
}

refreshJavadoc.dependsOn cleanJavadoc
refreshJavadoc.dependsOn allJavadoc

task wrapper(type: Wrapper) {
    gradleVersion = '4.2'
}
